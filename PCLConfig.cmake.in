@PACKAGE_INIT@

list(APPEND CMAKE_MODULE_PATH "${CMAKE_CURRENT_LIST_DIR}/Modules")

include(CMakeFindDependencyMacro)

@PCLCONFIG_EXTERNAL_DEPENDENCIES@

@PCLCONFIG_OPTIONAL_DEPENDENCIES@

if(@WITH_OPENMP@)
  find_dependency(OpenMP)
endif()

set(PCLComponents @PCLCONFIG_AVAILABLE_COMPONENTS@)

foreach(_component ${PCLComponents})
  foreach(_externComponent ${pcl_${_component}_ext_dep})
    find_dependency(${_externComponent})
  endforeach()
  foreach(_optComponent ${pcl_${_component}_opt_dep})
    find_package(${_optComponent})
    if(NOT ${_optComponent}_FOUND)
      message(STATUS "Optional dependency ${_optComponent} for PCL component ${_component} not found.")
    endif()
  endforeach()
endforeach()

include("${CMAKE_CURRENT_LIST_DIR}/PCLTargets.cmake")

# iterate through components attempting to be found
foreach(component ${${CMAKE_FIND_PACKAGE_NAME}_FIND_COMPONENTS})
  # if we couldn't find the component, set the phrases library to no
  # longer be found and notify the user about the missing component
  if (NOT ${component} IN_LIST PCLComponents)
    set(PCL_FOUND False)
    set(PCL_NOT_FOUND_MESSAGE "Unsupported component: ${component}")
  endif()
endforeach()
